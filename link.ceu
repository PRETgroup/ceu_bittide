native/pre do
    #include "link_fifo.c"
end
native/plain
    _LinkQueue,
    _link_make_queue,
    _link_queue_read,
    _link_queue_write,
    _link_queue_peek,
    _link_get_tail_time,
    _make_linkframe,
    _LinkFrame,
    _MIN
;
code/await Link(var& Buffer buffA, var& Buffer buffB, var& _double global_time, var _double latency, event& _double time_event)-> 
    (var _double next_clock_tick_time)-> NEVER do
    var int aToB = _link_make_queue(100);
    var int bToA = _link_make_queue(100);
    next_clock_tick_time = _DBL_MAX;

    code/call UpdateNextTickTime(none)->none do
        outer.next_clock_tick_time = _MIN(_link_get_tail_time(outer.aToB), _link_get_tail_time(outer.bToA));
    end

    par/and do
        loop do //move from link a->b to node b
            var _double simulation_time = await time_event;
            var _double exec_time = _link_get_tail_time(aToB);
            if (simulation_time >= exec_time) then
                //move frame to receiving buffer
                var _LinkFrame nextLF = _link_queue_read(aToB);
                buffB.receiveData = nextLF.frame;
                emit buffB.receive;
                call UpdateNextTickTime();
            end
        end
    with
        loop do //move from link b->a to node a
            var _double simulation_time = await time_event;
            var _double exec_time = _link_get_tail_time(bToA);
            if (simulation_time >= exec_time) then
                //move frame to receiving buffer
                var _LinkFrame nextLF = _link_queue_read(bToA);
                buffA.receiveData = nextLF.frame;
                emit buffA.receive;
                call UpdateNextTickTime();
            end
        end
    with
        loop do //
            await buffA.send;
            var _BittideFrame packet_contents = buffA.sendData;
            //schedule at current time + latency
            var _LinkFrame lf = _make_linkframe(packet_contents, global_time + latency);
            _link_queue_write(aToB,lf);
            call UpdateNextTickTime();
            
        end
    with 
        loop do
            await buffB.send;
            var _BittideFrame packet_contents = buffB.sendData;
            var _LinkFrame lf = _make_linkframe(packet_contents, global_time + latency);
            _link_queue_write(bToA,lf);
            call UpdateNextTickTime();
        end
    end
    await FOREVER;
end